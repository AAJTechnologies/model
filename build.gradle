project.ext {
    nibiruId = 'model'
    guavaVersion = '19.0'
}

allprojects {
    version = '1.0.1'
    group = 'io.github.nibiruos.model'

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven {
            url "https://oss.sonatype.org/content/groups/staging/"
        }
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        maven {
            url "https://oss.sonatype.org/content/repositories/google-snapshots/"
        }
    }
    buildscript {
        repositories {
            mavenCentral()
        }
        dependencies {
            classpath 'me.tatarka:gradle-retrolambda:3.4.0'
        }
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifact sourcesJar
                artifact javadocJar
                pom {
                    name = 'Nibiru ' + project.name
                    description = 'A set of classes for handling async tasks such as promises, UI loop and timers in a platform-independent way.'
                    url = 'https://github.com/NibiruOS/' + nibiruId + '/tree/master/' + project.name
                    licenses {
                        license {
                            name = 'The Apache Software License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'lbrasseur'
                            name = 'Lautaro Brasseur'
                            email = 'lbrasseur@gmail.com'
                        }
                    }
                    scm {
                        connection = 'https://github.com/NibiruOS/' + nibiruId + '.git'
                        developerConnection = 'https://github.com/NibiruOS/' + nibiruId + '.git'
                        url = 'https://github.com/NibiruOS/' + nibiruId
                    }
                }
            }
        }
        repositories {
            maven {
                credentials {
                    username = sonatypeUser
                    password = sonatypePassword
                }
                url version.endsWith('SNAPSHOT') ? 'https://oss.sonatype.org/content/repositories/snapshots' : 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
            }
        }
    }

    signing {
        sign publishing.publications.mavenJava
    }

    sourceCompatibility = 1.8

    dependencies {
        testCompile "junit:junit:4.12"
    }
}